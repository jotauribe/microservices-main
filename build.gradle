plugins {
  id 'application'
  id "com.github.johnrengelman.shadow" version "4.0.1"
}

repositories {
  jcenter()
  mavenCentral()
  maven {
    url "https://plugins.gradle.org/m2/"
  }
}

version = '1.0-SNAPSHOT'
sourceCompatibility = '1.8'
mainClassName = 'io.vertx.core.Launcher'

def vertxVersion = '3.5.1'
def mainVerticleName = 'io.vertx.starter.MainVerticle'
def watchForChange = 'src/**/*'
def doOnChange = './gradlew classes'

dependencies {
  compile "io.vertx:vertx-core:$vertxVersion"
  compile "io.vertx:vertx-mysql-postgresql-client:$vertxVersion"
  compile "io.vertx:vertx-web:$vertxVersion"
  compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.1'

  testCompile "junit:junit:4.12"
  testCompile "io.vertx:vertx-unit:$vertxVersion"
  testCompile group: 'org.mockito', name: 'mockito-core', version: '2.18.0'
  testCompile "io.vertx:vertx-codegen:$vertxVersion"
}

shadowJar {
  classifier = 'fat'
  manifest {
    attributes "Main-Verticle": mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

run {
  args = ['run', mainVerticleName, "--redeploy=$watchForChange", "--launcher-class=$mainClassName", "--on-redeploy=$doOnChange"]
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.0'
}
